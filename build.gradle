allprojects {
    apply plugin: 'groovy'

    group = 'se.diabol.scrolls'
    version = '0.0.1-SNAPSHOT'

    project.sourceCompatibility = 1.8
    project.targetCompatibility = 1.8

    repositories {
        jcenter()
    }

    dependencies {
        compile group: 'org.codehaus.groovy', name: 'groovy-all', version:'2.4.7'
    }
}

apply plugin: 'maven'
apply plugin: 'application'

description = 'An Automated Delivery Report Engine'

mainClassName = 'se.diabol.scrolls.engine.Scrolls'

dependencies {
    compile project(':plugin-api')

    compile group: 'org.freemarker', name: 'freemarker', version:'2.3.25-incubating'
    compile group: 'org.codehaus.groovy.modules.http-builder', name: 'http-builder', version:'0.7.1'
    compile group: 'commons-cli', name: 'commons-cli', version:'1.3.1'
    compile group: 'com.mashape.unirest', name: 'unirest-java', version:'1.4.9'

    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'org.spockframework', name: 'spock-core', version:'1.0-groovy-2.4'
    testCompile group: 'com.sun.grizzly', name: 'grizzly-comet-webserver', version:'1.9.64'
}

task uberjar(type: Jar, dependsOn:[':compileJava',':compileGroovy']) {
    from files(sourceSets.main.output.classesDir)
    from files(sourceSets.main.resources)
    from configurations.runtime.asFileTree.files.collect { zipTree(it) }

    manifest {
        attributes 'Main-Class': mainClassName
    }
}

run {
    /* Need to split the space-delimited value in the run.args */
    args System.getProperty('run.args', '').split()
}